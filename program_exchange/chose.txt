DIF:=EMA(CLOSE,12)-EMA(CLOSE,26);
DEA:=EMA(DIF,9);
MACD:=(DIF-DEA)*2;

SOCER1:=IF(MACD>0,IF(MACD > REF(MACD,1),25,12),IF(MACD <REF(MACD,1),-25,-12));
SOCER2:=IF(DIF>0,IF(DIF > REF(DIF,1),25,12),IF(DIF <REF(DIF,1),-25,-12));

CHANGEHAND := VOL/CAPITAL;
CHANGEHANDSOCER := IF(CHANGEHAND>0.08 AND (CLOSE > MA.MA1) AND (CLOSE < REF(CLOSE,20)*1.35),35,IF(CHANGEHAND>0.05 AND (CLOSE > MA.MA1) AND (CLOSE < REF(CLOSE,20)*1.35),25,0));



AVGVOL := MA(VOL,3);
AVGVOL2 := MA(VOL,20);
没有大涨 := CLOSE < REF(CLOSE,20)*1.35;
VOLSOCER := IF(AVGVOL/AVGVOL2>2 AND 没有大涨,40,IF(AVGVOL/AVGVOL2>1.68 AND 没有大涨,28,IF(AVGVOL/AVGVOL2>1.4 AND 没有大涨,10,0)));


NEWBREAK := IF (HHV(CLOSE,7) < HHV(CLOSE,20)*0.95,-50,IF (HHV(CLOSE,7) < HHV(CLOSE,20),-30,0));



DKSUB := IF (MA.MA1 < MA.MA2 AND MA.MA2 < MA.MA3 AND CLOSE < MA.MA2*0.97,-25,0);
DKADD := IF (MA.MA1 > MA.MA2 AND MA.MA2 > MA.MA3 AND CLOSE > MA.MA2*1.03,+25,0);

SOCER3:=IF(CLOSE<REF(LLV(LOW,10),1),IF((OPEN - CLOSE)/OPEN > 0.05,-40,-20),0);
SOCER4:=IF(CLOSE<REF(LLV(LOW,20),1),-40,0);

SOCERCLOSEMA20 := IF(CLOSE > MA.MA3*1.07,30,IF(CLOSE > MA.MA3*1.04,25,IF(CLOSE > MA.MA3*1.03,20,IF(CLOSE > MA.MA3,10,IF(CLOSE < MA.MA3 *0.94,-30,IF(CLOSE < MA.MA3*0.97,-20,-15))))));
SOCERMA5_20 := IF( MA.MA1 > MA.MA3*1.07,30,IF(MA.MA1 > MA.MA3*1.04,25,IF(MA.MA1 > MA.MA3*1.03,20,IF(MA.MA1 > MA.MA3,10,IF(MA.MA1 < MA.MA3 *0.94,-30,IF(MA.MA1 < MA.MA3*0.97,-20,-15))))));


SOCERMA5:= IF (CLOSE >MA.MA1,15,-15);
SOCERMA10:= IF (CLOSE > MA.MA2,15,-15);
UPSHADOWRATE:=MA(HIGH-MAX(CLOSE,OPEN),3)/REF(CLOSE,1);
AVGBODY:=MA(ABS(CLOSE-OPEN),6);
UPSHADOWRATE2 := (REF(HIGH,1) - MAX(REF(CLOSE,1),REF(OPEN,1)))/REF(CLOSE,1);
UPSHADOWSOCER0 := 0-IF(UPSHADOWRATE > 0.03,(UPSHADOWRATE-0.03)*2000,0);
UPSHADOWSOCER2 := 0-IF(UPSHADOWRATE2 > 0.03,(UPSHADOWRATE2-0.03)*3000,0);
BIAS2 := (CLOSE - MA.MA4)/MA.MA4;
UPSHADOWSOCER := IF(BIAS2 > 0, (UPSHADOWSOCER0 + UPSHADOWSOCER2)*BIAS2*2,0);


SOCERBEILI1 := IF(DIF>0 AND DIF < HHV(DIF,30)*0.618 AND COUNT(MACD<0,30)>=14 AND DIF < REF(DIF,1),-30,0);
SOCERBEILI2 := IF(MACD>0 AND MACD < HHV(MACD,30)*0.618 AND COUNT(MACD<0,30)>=14 AND MACD < REF(MACD,1),-20,0);
SOCKERBEILI := SOCERBEILI1+SOCERBEILI2;
SOCKERBEILI := IF(MACD<0,SOCKERBEILI*1.2,SOCKERBEILI);

DIBEILI := IF(MACD>0 AND DIF > REF(DIF,1) AND MACD > REF(MACD,1) AND DIF > LLV(DIF,30)*0.6 AND LLVBARS(DIF,30) > 10 AND COUNT(MACD>0,30)>=7 AND LLV(DIF,30) < 0 AND LLVBARS(DIF,40) > BARSLAST(MACD>0 AND REF(MACD,1) < 0)+15 ,35,0) ;
SOCER11:=IF(MA(CLOSE,5)<MA(CLOSE,20)*0.985,-30,0);



BIASSUB := IF( BIAS2 - 0.15>0,(0.15 - BIAS2)*300,0);

DAYCOUNT := IF(FROMOPEN >= 135 || CURRBARSCOUNT >= 1, 0, 1);
TDPRESELL := REF(CLOSE,8 + DAYCOUNT) > REF(CLOSE,12 + DAYCOUNT) && REF(CLOSE,9 + DAYCOUNT) < REF(CLOSE,13 + DAYCOUNT);
TDSELL7 := REF(CLOSE,DAYCOUNT) > REF(CLOSE,4+DAYCOUNT) && REF(CLOSE,1+DAYCOUNT) > REF(CLOSE,5 + DAYCOUNT) && REF(CLOSE,2+DAYCOUNT) > REF(CLOSE,6 + DAYCOUNT)
&& REF(CLOSE,3 + DAYCOUNT) > REF(CLOSE,7 + DAYCOUNT) && REF(CLOSE,4+DAYCOUNT) > REF(CLOSE,8+DAYCOUNT) && REF(CLOSE,5+DAYCOUNT) > REF(CLOSE,9+DAYCOUNT)
&& REF(CLOSE,6+DAYCOUNT) > REF(CLOSE,10+DAYCOUNT);
TDSELL8 := TDSELL7 && REF(CLOSE,7+DAYCOUNT) > REF(CLOSE,11+DAYCOUNT);
TDSELL9 := TDSELL8 && REF(CLOSE,8+DAYCOUNT) > REF(CLOSE,12+DAYCOUNT);
TDBUYPRE := REF(CLOSE,8 + DAYCOUNT) < REF(CLOSE,12 + DAYCOUNT) && REF(CLOSE,9 + DAYCOUNT) > REF(CLOSE,13 + DAYCOUNT);
TDBUY7PRE := REF(CLOSE,6 ) < REF(CLOSE,10 ) && REF(CLOSE,7 ) > REF(CLOSE,11 );
TDBUY8PRE := REF(CLOSE,7 ) < REF(CLOSE,11 ) && REF(CLOSE,8 ) > REF(CLOSE,12 );
TDBUY9PRE := REF(CLOSE,8 ) < REF(CLOSE,12 ) && REF(CLOSE,9 ) > REF(CLOSE,13 );


TDBUY7 :=  REF(CLOSE,0) < REF(CLOSE,4) && REF(CLOSE,1) < REF(CLOSE,5 ) && REF(CLOSE,2) < REF(CLOSE,6 )
&& REF(CLOSE,3) < REF(CLOSE,7) && REF(CLOSE,4) < REF(CLOSE,8) && REF(CLOSE,5) < REF(CLOSE,9) && REF(CLOSE,6) < REF(CLOSE,10);
TDBUY8 := TDBUY7 && REF(CLOSE,7) < REF(CLOSE,11);
TDBUY9 := TDBUY8 && REF(CLOSE,8) < REF(CLOSE,12);
TDSUB := IF(TDSELL9 == 1 OR REF(TDSELL9,1)==1 OR REF(TDSELL9,2)==1 OR REF(TDSELL9,2) ,-40,0);

LOWHIGHCOUNT := COUNT(HIGH>REF(HIGH,1) AND LOW > REF(LOW,1),4);
LOWHIGHCOUNTADD := IF(LOWHIGHCOUNT>=1,LOWHIGHCOUNT*10,0);

LOWHIGHCOUNT2 := COUNT(HIGH<REF(HIGH,1) AND LOW < REF(LOW,1),4);
LOWHIGHCOUNT2ADD := IF(LOWHIGHCOUNT2>=1,LOWHIGHCOUNT2*-10,0);
LOWHIGHCOUNTSOCER:= LOWHIGHCOUNTADD + LOWHIGHCOUNT2ADD;

BESTBUY := IF(CLOSE > OPEN AND UPSHADOWRATE < 0.028 AND LOW > REF(LOW,1) AND 没有大涨 AND DIF > REF(DIF,1) AND MACD > 0 AND CLOSE > MA.MA3*1.02 AND CLOSE < MA.MA2 *1.08,30,-10 );
SOCERDAY:=SOCER1+SOCER2+SOCER3+SOCER4 + SOCERCLOSEMA20+SOCERMA5_20+DKADD+VOLSOCER+NEWBREAK+DKSUB+SOCER11+SOCERMA5+SOCERMA10+ SOCKERBEILI + DIBEILI + BIASSUB + TDSUB + CHANGEHANDSOCER + LOWHIGHCOUNTSOCER + BESTBUY;

CHANGEHANDWEEK := VOL#WEEK/CAPITAL#WEEK;
CHANGEHANDSOCERWEEK := IF( CHANGEHANDWEEK > 0.3,70,IF(CHANGEHANDWEEK>0.2,40,IF(CHANGEHANDWEEK >0.12,20,0)));
CHANGEHANDSOCERWEEK := IF( (CLOSE#WEEK > MA.MA1#WEEK AND CLOSE < REF(CLOSE,20)*1.35),CHANGEHANDSOCERWEEK,0);
monthmacd := macd.macd#MONTH(12,26,9);
SOCERMACDWEEK := IF(macd.MACD#WEEK>0,20,-50);
weekrsi1 := rsi.RSI1#WEEK(6,12,24);
weekrsi2 := rsi.RSI2#WEEK(6,12,24);
weekrsi3 := rsi.RSI3#WEEK(6,12,24);
closeweek := CLOSE#WEEK;
SOCER16 := IF(CLOSE#WEEK>ma.MA3#WEEK,25,-25);
SOCER17 := IF(ma.MA1#WEEK>ma.MA3#WEEK,30,-30);
SOCERMACDDIF := IF(macd.DIF#WEEK < 0,-50,0);
SOCERMACDDEA := IF(macd.DEA#WEEK < 0,-40,0);
SOCERWEEK30 := IF(ma.MA4#WEEK<REF(ma.MA4#WEEK,5),-35,10);
SOCERWEEKRSI2 := IF (rsi.RSI2#WEEK < 50,-20,0);
SOCERWEEKRSI1 := IF (rsi.RSI1#WEEK < 50,-25,0);
SOCERWEEKRSI3 := IF (rsi.RSI3#WEEK < 50,-30,0);


DIBEILIWEEK := IF(macd.MACD#WEEK>0 AND macd.DIF > REF(macd.DIF,5) AND macd.MACD#WEEK > REF(macd.MACD#WEEK,5) AND macd.DIF > LLV(macd.DIF,150)*0.6 AND LLVBARS(macd.DIF,150) > 10 AND COUNT(macd.MACD>0,150)>=7 AND LLV(macd.DIF,150) < 0 AND LLVBARS(macd.DIF,200) > BARSLAST(macd.MACD>0 AND REF(macd.MACD,5) < 0)+15 ,50,0) ;
VOLWEEK0 := IF (CLOSE#WEEK > ma.MA2#WEEK AND (VOL#WEEK + REF(VOL#WEEK,5)) > 2.68*(REF(VOL#WEEK,20)+REF(VOL#WEEK,30)),20,0 );
VOLWEEK1 := IF (CLOSE#WEEK > ma.MA2#WEEK AND (VOL#WEEK + REF(VOL#WEEK,5)) > 2*(REF(VOL#WEEK,20)+REF(VOL#WEEK,30)),20,0 );
VOLWEEK2 := IF (CLOSE#WEEK > ma.MA2#WEEK AND (VOL#WEEK + REF(VOL#WEEK,5)) > 1.68*(REF(VOL#WEEK,20)+REF(VOL#WEEK,30)),15,0 );
VOLWEEK := VOLWEEK0 + VOLWEEK1 + VOLWEEK2;
SOCERWEEK :=  SOCERMACDWEEK + SOCER16 + SOCER17 + SOCERMACDDIF + SOCERWEEKRSI1 + SOCERWEEKRSI2 + SOCERWEEKRSI3 + SOCERMACDDEA + SOCERWEEK30 + DIBEILIWEEK + VOLWEEK + CHANGEHANDSOCERWEEK;

WEEKSIGN0:=IF(ma.MA2#WEEK>REF(ma.MA2#WEEK,5),10,-10);
WEEKSIGN:=IF(ma.MA2#WEEK>REF(ma.MA2#WEEK,5) && REF(ma.MA2#WEEK,5) > REF(ma.MA2#WEEK,10) && REF(ma.MA2#WEEK,10) > REF(ma.MA2#WEEK,15),30,-10 );
WEEKSIGN2:=IF(ma.MA2#WEEK<REF(ma.MA2#WEEK,5) && REF(ma.MA2#WEEK,5) < REF(ma.MA2#WEEK,10) && REF(ma.MA2#WEEK,10) < REF(ma.MA2#WEEK,15),-30,10 );
WEEKSIGN3:=IF(ma.MA1#WEEK>ma.MA2#WEEK && ma.MA2#WEEK > ma.MA3#WEEK,40, -10 );
WEEKSIGN4:=IF(ma.MA1#WEEK<ma.MA2#WEEK && ma.MA2#WEEK < ma.MA3#WEEK,-40,0 );

WEEKSIGN5 := 0;
WEEKSIGN6:=IF(ma.MA3#WEEK<REF(ma.MA3#WEEK,5),-15,0);
SOCERWEEK:=SOCERWEEK+WEEKSIGN+WEEKSIGN2+WEEKSIGN3+WEEKSIGN4+WEEKSIGN5+WEEKSIGN6+WEEKSIGN0;

SOCERTOTAL:=SOCERDAY+SOCERWEEK;
DIESIGN := IF( SOCERTOTAL + REF(SOCERTOTAL,1) < REF(SOCERTOTAL,2) + REF(SOCERTOTAL,3) - 300 AND SOCERTOTAL < REF(SOCERTOTAL,1)-100, 1,0);

//BAODIE:=IF( (SOCERTOTAL < REF(SOCERTOTAL,1) - 100), 1,0) && REF(SOCERTOTAL,1) > 0;
BAODIE:=IF( (SOCERTOTAL < REF(SOCERTOTAL,1) - 188), 1,0);
是指数:=FINANCE(20) == 0;

BUYSIGN := IF(是指数,SOCERWEEK > 5 AND SOCERDAY > 30 && REF(SOCERTOTAL,1) > 0 &&SOCERTOTAL>60 && (REF(SOCERTOTAL,1)+SOCERTOTAL >= 100) && SOCERTOTAL > REF(SOCERTOTAL,1)-70 && BAODIE == 0 && REF(BAODIE,1) == 0 && REF(BAODIE,2) == 0 && REF(BAODIE,3) == 0,
SOCERWEEK > 40 AND SOCERDAY > 90 && REF(SOCERTOTAL,1) > 0 && SOCERTOTAL>190 && (REF(SOCERTOTAL,1)+SOCERTOTAL >= 250) && SOCERTOTAL > REF(SOCERTOTAL,1)-100 && BAODIE == 0 && REF(BAODIE,1) == 0 && REF(BAODIE,2) == 0 && REF(BAODIE,3) == 0
);


KONGSIGN := SOCERTOTAL<-25 && REF(SOCERTOTAL,1) > 0;
GREENSIGN := BAODIE = 1 OR KONGSIGN = 1 OR DIESIGN = 1;

KDJTDLIMIT:= IF(是指数,70,90);

TSIGN := IF(是指数,BUYSIGN == 0 AND SOCERTOTAL >=35 AND GREENSIGN == 0 AND REF(GREENSIGN,1) == 0 AND REF(GREENSIGN,2) == 0 AND ((SOCERWEEK >=-10 AND SOCERDAY >= 25)),
BUYSIGN == 0 AND SOCERTOTAL >=120 AND GREENSIGN == 0 AND REF(GREENSIGN,1) == 0 AND (SOCERWEEK >=20 AND SOCERDAY >= 60));



XIONGSHI := GREENSIGN =0 AND BUYSIGN =0 AND TSIGN = 0;
CHOSE := BUYSIGN == 1 AND SOCERTOTAL > 300 AND 没有大涨 AND REF(OPEN,1) > REF(CLOSE,1) AND LOW > REF(LOW,1) AND CLOSE > REF(CLOSE,1);
CHOSE;




